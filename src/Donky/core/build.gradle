apply plugin: 'com.android.library'

// ./gradlew clean build generateRelease
apply plugin: 'maven'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        disable 'InvalidPackage'
        disable 'MissingPermission'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile'junit:junit:4.12'
    compile 'com.google.android.gms:play-services-gcm:8.4.0'
    compile 'com.squareup.retrofit:retrofit:1.9.0'
    compile 'com.squareup.okhttp:okhttp:2.4.0'
}

// Release part

def groupId = 'net.donky'
def artifactId = 'donky-core'
def version = '2.7.0.1'

def localReleaseDest = "${buildDir}/release/${version}"

task androidJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
    classifier = 'javadoc'
    from androidJavadocs.destinationDir
}

task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

uploadArchives {
    repositories.mavenDeployer {
        pom.groupId = groupId
        pom.artifactId = artifactId
        pom.version = version
        pom.project {

            packaging 'aar'

            licenses {
                license {
                    name 'MIT License'
                    url 'http://www.opensource.org/licenses/mit-license.php'
                    distribution 'repo'
                }
            }
            name 'Donky Core'
            description 'Provides the basics of any Donky integration into your apps. The Core SDK is responsible for handling: registration onto the Donky Network, control of User and Device registration details, sending and receiving content & data.'
            url 'http://www.mobiledonky.com/'

            developers {
                developer {
                    id 'marcinswierczek'
                    name 'Marcin Swierczek';
                    email 'sdk@mobiledonky.com';
                }
            }

            scm {
                url 'https://github.com/Donky-Network/DonkySDK-Android-Modular'
                connection 'scm:git:https://github.com/Donky-Network/DonkySDK-Android-Modular.git'
                developerConnection 'scm:git:https://github.com/Donky-Network/DonkySDK-Android-Modular.git'
            }
        }

        // Add other pom properties here if you want (developer details / licenses)
        repository(url: "file://${localReleaseDest}")
    }
}

task zipRelease(type: Zip) {
    from localReleaseDest
    destinationDir buildDir
    archiveName "${artifactId}-${version}.zip"
}

task generateRelease << {
    println "Release ${version} can be found at ${localReleaseDest}/"
    println "Release ${version} zipped can be found ${buildDir}/release-${version}.zip"
}

generateRelease.dependsOn(uploadArchives)
generateRelease.dependsOn(zipRelease)

artifacts {
    archives androidSourcesJar
    archives androidJavadocsJar
}
